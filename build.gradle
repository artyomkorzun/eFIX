plugins {
    id 'java'
    id 'maven-publish'
    id 'me.champeau.gradle.jmh' version '0.2.0'
}

group = 'org.efix'
version = '0.2.6'

repositories {
    mavenCentral()
}

configurations {
    deployerJars
}

dependencies {
    jmh group: 'org.hdrhistogram', name: 'HdrHistogram', version: '2.1.9'
    jmh group: 'net.openhft', name: 'affinity', version: '3.0.6'
    jmh group: 'org.agrona', name: 'Agrona', version: '0.9.1'

    testCompile 'junit:junit:4.12'
}

compileJava {
    // Suppress warnings about using Unsafe and sun.misc
    options.compilerArgs << '-XDignore.symbol.file'
    options.fork = true
    options.forkOptions.executable = 'javac'

    sourceCompatibility = 1.8
    targetCompatibility = 1.8
}

task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

artifacts {
    archives sourcesJar
    //archives javadocJar
}

publishing {
    repositories {
        maven {
            url publishRepositoryUrl
            credentials {
                username publisherUsername
                password publisherPassword
            }
        }
    }

    publications {
        mavenJava(MavenPublication) {
            from components.java
            artifact sourcesJar
        }
    }
}

jmhJar {
    outputs.upToDateWhen { false }
}

jmh {
    jmhVersion = '1.18'
    include = 'MessageParser'
    benchmarkMode = 'avgt'
    timeUnit = 'ns'
    warmup = '3s'
    warmupIterations = 2
    timeOnIteration = '5s'
    iterations = 3
    threads = 1
    fork = 1
    jvmArgsAppend = '-Defix.disable.bounds.check=true'
}